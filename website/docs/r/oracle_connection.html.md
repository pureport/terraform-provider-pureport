---
layout: "pureport"
page_title: "Pureport: pureport_oracle_connection"
sidebar_current: "docs-pureport-resource-oracle_connection"
description: |-
  Manages a Pureport Oracle Fast Connect Connection.
---

# Resource: pureport\_oracle\_connection

## Example Usage

```hcl

data "pureport_accounts" "main" {
  filter {
    name = "Name"
    values = ["Terraform"]
  }
}

data "pureport_locations" "main" {
  filter {
    name = "Name"
    values = ["^Washington*"]
  }
}

data "pureport_networks" "main" {
  account_href = data.pureport_accounts.main.accounts.0.href
  filter {
    name = "Name"
    values = ["Bansh.*"]
  }
}

data "pureport_cloud_regions" "main" {
  filter {
    name = "DisplayName"
    values = ["Ashburn"]
  }
}

resource "pureport_oracle_connection" "basic" {
  name = "My Oracle Connection"
  speed = "1000"

  location_href = data.pureport_locations.main.locations.0.href
  network_href = data.pureport_networks.main.networks.0.href
  cloud_region_href = data.pureport_cloud_regions.main.regions.0.href

  primary_ocid = "ocid1.instance.oc1.phx.abuw4ljrlsfiqw6vzzxb4300000000000000000000000000000000000001"
  secondary_ocid = "ocid1.instance.oc1.phx.abuw4ljrlsfiqw6vzzxb4300000000000000000000000000000000000002"

  bgp_peering {
    availability_domain = "PRIMARY"
    pureport_subnet = "169.254.24.1/30"
    customer_subnet = "169.254.24.2/30"
  }

  bgp_peering {
    availability_domain = "SECONDARY"
    pureport_subnet = "169.254.24.5/30"
    customer_subnet = "169.254.24.6/30"
  }

  tags = {
    Environment = "tf-test"
    sweep       = "TRUE"
  }
}
```

## Argument Reference

The following arguments are supported:

* `name` - (Required) The name for the connection
* `location_href` - (Required) HREF for the Pureport Location to attach the connection.
* `network_href` - (Required) HREF for the network to associate the connection.
* `speed` - (Required) The maximum QoS for this connection. Valid values are 1000, 2000 in Mbps.
* `primary_ocid` - (Required) Primary Oracle Virtual Circuit OCID.
* `secondary_ocid` - (Required) Secondary Oracle Virtual Circuit OCID.
* `bgp_peering` - (Required) List of BGP Subnet mapping for primary and secondary gateways.

- - -

* `description` - (Optional) The description for the connection.
* `customer_networks` - (Optional) A list of named CIDR block to easily identify a customer network.
    * `name` - The name for the network.
    * `address` - The CIDR block for the network
* `nat_config` - (Optional) The Network Address Translation configuration for the connection.
    * `enabled` - (Required) Is NAT enabled for this connection.
    * `mappings` - (Optional) List of NAT mapped CIDR address
        * `native_cidr` - (Required) The native CIDR block to map.
* `billing_term` - (Optional) The billing term for the connection: (Currently only HOURLY is supported.)
* `tags` - (Optional) A dictionary of user defined key/value pairs to associate with this resource.


## Attributes

* `nat_config` - The Network Address Translation configuration for the connection.
    * `enabled` - Is NAT enabled for this connection.
    * `mappings` - List of NAT mapped CIDR address
        * `native_cidr` - The native CIDR block to map.
        * `nat_cidr` - The CIDR block use for NAT to the associated subnet.
    * `blocks` - List of reserved blocks for NAT.
    * `pnat_cidr` - CIDR use for PNAT between connections.

* `gateways` - List of cloud gateways and their configurations.

    * `name` - The name of the cloud gateway.

    * `description` - The description of the cloud gateway.

    * `availability_domain` - The availability domain of the cloud gateway. The valid values are `PRIMARY`, `SECONDARY`.

    * `customer_asn` - The customer ASN used for BGP Peering.

    * `customer_ip` - The assigned IP address to the customer side of the BGP Config.

    * `pureport_asn` - The Pureport ASN used for BGP Peering.

    * `pureport_ip` -  The assigned IP address to the Pureport side of the BGP Config.

    * `bgp_password` - The autogenerated BGP password used for authentication. (**NOT SUPPORTED**)

    * `peering_subnet` - The BGP Config subnet assigned to establish BGP peering.

    * `public_nat_ip` - The public facing IP Address for NAT used by this connection.

    * `remote_id` - The ID of the Oracle Direct Connect Connection.

    * `vlan` - The VLAN id for the connection to cloud services.

The Pureport Guide, []()
